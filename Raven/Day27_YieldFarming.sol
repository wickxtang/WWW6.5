// SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/utils/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/math/SafeCast.sol";

// Optional interface from ERC20
interface IERC20Metadata is IERC20 {
	function decimals() external view returns (uint8);
	function name() external view returns (string memory);
	function symbol() external view returns (string memory);
}
contract YieldFarming is ReentrancyGuard {
	// Safe cast for toUint128()、toUint64()
	using SafeCast for uint256;
	IERC20 public stakingToken;
	IERC20 public rewardToken;
	uint256 public rewardRatePerSecond;
	address public owner;
	uint8 public stakingTokenDecimals;
	struct StakeInfo {
		uint256 stakedAmount;
		uint256 rewardDebt;
		uint256 lastUpdate;
	}
	mapping(address => StakeInfo) public stakers;
    event Staked(address indexed user, uint256 amount);
    event Unstaked(address indexed user, uint256 amount);
    event RewardClaimed(address indexed user, uint256 amount);
    event EmergencyWithdraw(address indexed user, uint256 amount);
    event RewardRefilled(address indexed owner, uint256 amount);

    modifier onlyOwner() {
        require(msg.sender == owner, "Not the owner");
        _;
    }
	constructor(
		address _stakingToken,
		address _rewardToken,
		uint256 _rewardRatePerSecond
	) {
		stakingToken = IERC20(_stakingToken);
		rewardToken = IERC20(_rewardToken);
		// Minimum unit of token
		rewardRatePerSecond = _rewardRatePerSecond;
		owner = msg.sender;
		try IERC20Metadata(_stakingToken).decimals() returns (uint8 decimals) {
			stakingTokenDecimals = decimals;
		} catch (bytes memory) {
			// If not defined metadata, use default dicimals
			stakingTokenDecimals = 18;
		}
	}
	// User stakes money into contract
	function stake(uint256 amount) external nonReentrant {
		require(amount > 0, "Invalid amount");
		// Update rewards till now before modify stake
		updateRewards(msg.sender);
		stakingToken.transferFrom(msg.sender, address(this), amount);
		stakers[msg.sender].stakedAmount += amount;
		emit Staked(msg.sender, amount);
	}
	// User removes staked money from contract
	function unstake(uint256 amount) external nonReentrant {
		require(amount > 0, "Invalid amount");
		require(stakers[msg.sender].stakedAmount >= amount, "Not enough staked");
		updateRewards(msg.sender);
		stakers[msg.sender].stakedAmount -= amount;
		stakingToken.transfer(msg.sender, amount);
		emit Unstaked(msg.sender, amount);
	}
	// User claims rewards without modify staked money
	function claimRewards() external nonReentrant {
		updateRewards(msg.sender);
		uint256 reward = stakers[msg.sender].rewardDebt;
		require(reward > 0, "No reward");
		require(rewardToken.balanceOf(address(this)) >= reward, "Insufficient reward balance");
		stakers[msg.sender].rewardDebt = 0;
		rewardToken.transfer(msg.sender, reward);
		emit RewardClaimed(msg.sender, reward);
	}
	// User withdraws all staked money and gives up all rewards
	function emergencyWithdraw() external nonReentrant {
		uint256 amount = stakers[msg.sender].stakedAmount;
		require(amount > 0, "Invalid amount");
		stakers[msg.sender].stakedAmount = 0;
		stakers[msg.sender].rewardDebt = 0;
		stakers[msg.sender].lastUpdate = block.timestamp;
		stakingToken.transfer(msg.sender, amount);
		emit EmergencyWithdraw(msg.sender, amount);
	}
	// Owner fills reward pool
	function refillRewards(uint256 amount) external onlyOwner {
		rewardToken.transferFrom(msg.sender, address(this), amount);
		emit RewardRefilled(msg.sender, amount);
	}
	// User updates rewards since last update
	function updateRewards(address user) internal {
		StakeInfo storage staker = stakers[user];
		if (staker.stakedAmount > 0) {
			uint256 timeDiff =block.timestamp - staker.lastUpdate;
			uint256 rewardMultiplier = 10 ** stakingTokenDecimals;
			uint256 pendingReward = (timeDiff * rewardRatePerSecond * staker.stakedAmount) / rewardMultiplier;
			staker.rewardDebt += pendingReward;
		}
		staker.lastUpdate = block.timestamp;
	}
	// View pending rewards since last update
	function pendingRewards(address user) external view returns (uint256) {
		StakeInfo memory staker = stakers[user];
		uint256 pendingReward = staker.rewardDebt;
		if (staker.stakedAmount > 0) {
			uint256 timeDiff =block.timestamp - staker.lastUpdate;
			uint256 rewardMultiplier = 10 ** stakingTokenDecimals;
			pendingReward += (timeDiff * rewardRatePerSecond * staker.stakedAmount) / rewardMultiplier;
		}
		return pendingReward;
	}
	function getStakingTokenDecimals() external view returns (uint8) {
		return stakingTokenDecimals;
	}
}